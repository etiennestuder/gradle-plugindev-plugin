plugins {
    id 'nu.studer.plugindev' version '1.0.12'
    id 'nu.studer.credentials' version '2.1'
    id 'groovy'
}

group = 'nu.studer'
version = '2.0'

dependencies {
    implementation 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    implementation 'nu.studer:java-ordered-properties:1.0.4'
    testImplementation 'org.spockframework:spock-core:2.0-M3-groovy-2.5'
    testImplementation 'org.spockframework:spock-junit4:2.0-M3-groovy-2.5'
}

configurations.all { configuration ->
    configuration.resolutionStrategy.eachDependency {
        if (requested.group == 'org.codehaus.groovy') {
            useVersion '2.5.12'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

plugindev {
    pluginDescription 'Gradle plugin that facilitates the bundling and uploading of Gradle plugins as expected by the Gradle Plugin Portal, JCenter, and MavenCentral.'
    pluginImplementationClass 'nu.studer.gradle.plugindev.PluginDevPlugin'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'plugindevelopment'
    authorId 'etiennestuder'
    authorName 'Etienne Studer'
    authorEmail 'etienne@studer.nu'
    projectUrl 'https://github.com/etiennestuder/gradle-plugindev-plugin'
    projectInceptionYear '2014'
    done()
}

bintray {
    user = credentials.BINTRAY_USER
    key = credentials.BINTRAY_API_KEY
    pkg.repo = 'gradle-plugins'
    dryRun = false
}

def testAll = tasks.create('testAll') {
    description = 'Runs the Gradle cross-version tests.'
    group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << "6.5"
testedGradleVersions << "6.0.1"

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}

tasks.withType(Test).configureEach {
    maxParallelForks = 2
    useJUnitPlatform()
}
