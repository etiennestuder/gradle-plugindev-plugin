plugins {
    id 'com.gradle.build-scan' version '3.3.1'
    id 'nu.studer.plugindev' version '1.0.12'
    id 'nu.studer.credentials' version '2.1'
    id 'groovy'
}

group = 'nu.studer'
version = '1.0.13-DEV'

buildScan {
    if (project.properties['gradle-plugindev-plugin.acceptGradleTOS']) {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
    publishAlways()
    obfuscation {
        ipAddresses { addresses -> addresses.collect { address -> '0.0.0.0' } }
    }
}

dependencies {
    implementation 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    implementation 'nu.studer:java-ordered-properties:1.0.2'
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude module: 'groovy-all'
    }
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.6.2'
}

plugindev {
    pluginDescription 'Gradle plugin that facilitates the bundling and uploading of Gradle plugins as expected by the Gradle Plugin Portal, JCenter, and MavenCentral.'
    pluginImplementationClass 'nu.studer.gradle.plugindev.PluginDevPlugin'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'plugindevelopment'
    authorId 'etiennestuder'
    authorName 'Etienne Studer'
    authorEmail 'etienne@studer.nu'
    projectUrl 'https://github.com/etiennestuder/gradle-plugindev-plugin'
    projectInceptionYear '2014'
    done()
}

bintray {
    user = credentials.BINTRAY_USER
    key = credentials.BINTRAY_API_KEY
    pkg.repo = 'gradle-plugins'
    dryRun = false
}

def testAll = tasks.create('testAll') {
    description = 'Runs the Gradle cross-version tests.'
    group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << "6.0.1"
testedGradleVersions << "5.6.4"
testedGradleVersions << "5.0"
testedGradleVersions << "4.10.3"
testedGradleVersions << "4.3"
testedGradleVersions << "4.0.2"

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}

tasks.withType(Test).configureEach {
    maxParallelForks = 2
    useJUnitPlatform() {
        includeEngines 'junit-vintage'
    }
}
