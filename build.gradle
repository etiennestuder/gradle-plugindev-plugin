plugins {
//    id 'nu.studer.plugindev' version '2.0'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'nu.studer.credentials' version '2.1'
    id 'org.nosphere.gradle.github.actions' version '1.2.0'
}

group = 'nu.studer'
version = '3.0-RC-1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'nu.studer:java-ordered-properties:1.0.4'

    testImplementation 'org.spockframework:spock-core:2.0-groovy-2.5'
    testImplementation 'org.spockframework:spock-junit4:2.0-groovy-2.5'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

def testAll = tasks.create('testAll') {
    description = 'Runs the Gradle cross-version tests.'
    group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << '7.1'
testedGradleVersions << '7.0.2'
testedGradleVersions << '6.9'
testedGradleVersions << '6.0.1'

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}

tasks.withType(Test).configureEach {
    maxParallelForks = 2
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        pluginDevPlugin {
            id = 'nu.studer.plugindev'
            implementationClass = 'nu.studer.gradle.plugindev.PluginDevPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/etiennestuder/gradle-plugindev-plugin'
    vcsUrl = 'https://github.com/etiennestuder/gradle-plugindev-plugin'
    description = 'Gradle plugin that facilitates the bundling and uploading of Gradle plugins as expected by the Gradle Plugin Portal.'
    tags = ['gradle', 'plugin', 'plugin development']

    plugins {
        pluginDevPlugin {
            displayName = 'gradle-plugindev-plugin'
        }
    }

    mavenCoordinates {
        groupId = 'nu.studer'
        artifactId = 'gradle-plugindev-plugin'
    }
}


//plugindev {
//    pluginDescription 'Gradle plugin that facilitates the bundling and uploading of Gradle plugins as expected by the Gradle Plugin Portal, JCenter, and MavenCentral.'
//    pluginImplementationClass 'nu.studer.gradle.plugindev.PluginDevPlugin'
//    pluginLicenses 'Apache-2.0'
//    pluginTags 'gradle', 'plugin', 'plugindevelopment'
//    authorId 'etiennestuder'
//    authorName 'Etienne Studer'
//    authorEmail 'etienne@studer.nu'
//    projectUrl 'https://github.com/etiennestuder/gradle-plugindev-plugin'
//    projectInceptionYear '2014'
//    done()
//}
