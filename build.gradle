plugins {
    id 'nu.studer.plugindev' version '4.0'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'org.nosphere.gradle.github.actions' version '1.2.0'
    id 'groovy'
}

group = 'nu.studer'
version = '4.0.1-DEV'

dependencies {
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    sourceCompatibility = null
    targetCompatibility = null
}

tasks.withType(AbstractCompile).configureEach {
    options.compilerArgs <<
        "-Werror" <<
        "-Xlint:all"
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler.set(javaToolchains.compilerFor { spec ->
        spec.languageVersion.set(JavaLanguageVersion.of(8))
        buildScan.tag 'COMPILE_JVM_' + spec.languageVersion.get().asInt()
    } as Provider<? extends JavaCompiler>)
}

tasks.withType(Test).configureEach {
    maxParallelForks = 2
    useJUnitPlatform()
    javaLauncher.set(javaToolchains.launcherFor { spec ->
        String testJavaRuntimeVersion = findProperty('testJavaRuntimeVersion') ?: '8'
        spec.languageVersion.set(JavaLanguageVersion.of(testJavaRuntimeVersion))
        buildScan.tag 'TEST_JVM_' + spec.languageVersion.get().asInt()
    } as Provider<? extends JavaLauncher>)
}

List<String> testedGradleVersions = []
testedGradleVersions << '7.2'
testedGradleVersions << '7.1.1'

def testTasks = testedGradleVersions.collect { version ->
    def tp = project.tasks.register('test_' + version.replaceAll("[^a-zA-Z0-9]", "_"), Test)
    tp.configure { t ->
        t.systemProperty 'testContext.gradleVersion', version
    }
    tp
}

def testAll = tasks.register('testAll') { all ->
    all.description = 'Runs the Gradle cross-version tests.'
    all.group = 'Verification'
    testTasks.each { t ->
        all.dependsOn t
    }
}

tasks.named('check').configure {
    if (System.getenv('GITHUB_ACTIONS')) {
        dependsOn testAll
    }
}

gradlePlugin {
    plugins {
        pluginDevPlugin {
            id = 'nu.studer.plugindev'
            implementationClass = 'nu.studer.gradle.plugindev.PluginDevPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/etiennestuder/gradle-plugindev-plugin'
    vcsUrl = 'https://github.com/etiennestuder/gradle-plugindev-plugin'
    description = 'Gradle plugin that facilitates the bundling of Gradle plugins as expected by the Gradle Plugin Portal.'
    tags = ['plugin development']

    plugins {
        pluginDevPlugin {
            displayName = 'gradle-plugindev-plugin'
        }
    }

    mavenCoordinates {
        groupId = 'nu.studer'
        artifactId = 'gradle-plugindev-plugin'
    }
}
