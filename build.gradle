plugins {
    id 'com.gradle.build-scan' version '1.16'
    id 'nu.studer.plugindev' version '1.0.11'
    id 'nu.studer.credentials' version '1.0.4'
    id 'groovy'
}

group = 'nu.studer'
version = '1.0.12-DEV'

buildScan {
    licenseAgreementUrl 'https://gradle.com/terms-of-service'
    licenseAgree 'yes'
}

dependencies {
    compile "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    compile "nu.studer:java-ordered-properties:1.0.1"
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

plugindev {
    pluginDescription 'Gradle plugin that facilitates the bundling and uploading of Gradle plugins as expected by the Gradle Plugin Portal, JCenter, and MavenCentral.'
    pluginImplementationClass 'nu.studer.gradle.plugindev.PluginDevPlugin'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'plugindevelopment'
    authorId 'etiennestuder'
    authorName 'Etienne Studer'
    authorEmail 'etienne@studer.nu'
    projectUrl 'https://github.com/etiennestuder/gradle-plugindev-plugin'
    projectInceptionYear '2014'
    done()
}

bintray {
    user = credentials.BINTRAY_USER
    key = credentials.BINTRAY_API_KEY
    pkg.repo = 'gradle-plugins'
    dryRun = false
}

test {
    maxParallelForks = 2
}

def testAll = tasks.create('testAll') {
    description = 'Runs the Gradle cross-version tests.'
    group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << "5.1"
testedGradleVersions << "5.0"
testedGradleVersions << "4.10.3"
testedGradleVersions << "4.3"
testedGradleVersions << "4.0.2"
testedGradleVersions << "3.5.1"

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}

