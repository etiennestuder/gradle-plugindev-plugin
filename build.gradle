plugins {
    id 'nu.studer.plugindev' version '4.0'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'org.nosphere.gradle.github.actions' version '1.2.0'
    id 'groovy'
}

group = 'nu.studer'
version = '4.0.1-DEV'

dependencies {
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    sourceCompatibility = null
    targetCompatibility = null
}

tasks.withType(AbstractCompile).configureEach {
    options.compilerArgs <<
        "-Werror" <<
        "-Xlint:all"
}

tasks.withType(Test).configureEach {
    maxParallelForks = 2
    useJUnitPlatform()
    javaLauncher.set(javaToolchains.launcherFor {
        String testJavaRuntimeVersion = findProperty('testJavaRuntimeVersion') ?: '8'
        languageVersion = JavaLanguageVersion.of(testJavaRuntimeVersion)
        buildScan.tag 'TEST_JVM_' + testJavaRuntimeVersion
    } as Provider<? extends JavaLauncher>)
}

def testAll = tasks.create('testAll') {
    description = 'Runs the Gradle cross-version tests.'
    group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << '7.2'
testedGradleVersions << '7.1.1'

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}

tasks.named('check').configure {
    if (System.getenv('GITHUB_ACTIONS')) {
        dependsOn testAll
    }
}

gradlePlugin {
    plugins {
        pluginDevPlugin {
            id = 'nu.studer.plugindev'
            implementationClass = 'nu.studer.gradle.plugindev.PluginDevPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/etiennestuder/gradle-plugindev-plugin'
    vcsUrl = 'https://github.com/etiennestuder/gradle-plugindev-plugin'
    description = 'Gradle plugin that facilitates the bundling of Gradle plugins as expected by the Gradle Plugin Portal.'
    tags = ['plugin development']

    plugins {
        pluginDevPlugin {
            displayName = 'gradle-plugindev-plugin'
        }
    }

    mavenCoordinates {
        groupId = 'nu.studer'
        artifactId = 'gradle-plugindev-plugin'
    }
}
